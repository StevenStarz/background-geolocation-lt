apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION     = 26
def DEFAULT_BUILD_TOOLS_VERSION     = "26.0.2"
def DEFAULT_TARGET_SDK_VERSION      = 26
def DEFAULT_PLAY_SERVICES_VERSION   = "11.8.0"

android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION

        versionCode 1
        versionName "0.0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories{
    jcenter()
    flatDir{
        dirs 'libs'
    }
    maven {
        url './libs'
    }
    maven {
        url 'https://maven.google.com'
    }
}

dependencies {
    def playServicesVersion = DEFAULT_PLAY_SERVICES_VERSION
    if (rootProject.hasProperty('playServicesVersion')) {
        playServicesVersion = rootProject.playServicesVersion
    } else if (rootProject.hasProperty('googlePlayServicesVersion')) {
        playServicesVersion = rootProject.googlePlayServicesVersion
    }

    api "com.google.android.gms:play-services-location:$playServicesVersion"
    api 'org.greenrobot:eventbus:3.0.0'
    api 'com.squareup.okhttp3:okhttp:3.10.0'
    api(group: 'com.transistorsoft', name:'tslocationmanager', version: '+')
    api 'org.slf4j:slf4j-api:1.7.21'
    api 'com.github.tony19:logback-android:1.1.1-9'
    // android-permissions
    api 'com.intentfilter:android-permissions:0.1.6'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

